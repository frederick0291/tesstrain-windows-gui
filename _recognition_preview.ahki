; (C) Copyright 2021, Bartlomiej Uliasz
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
; http://www.apache.org/licenses/LICENSE-2.0
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.

; Allows preview of OCR recognition after training is done

PreviewRecognition(btnCtrl, *) {
	closeRequest := false
	ocrInProgress := false
	searchEnabled:=false

	col1Width := 120
	col2Width := 680
	tessdataDirForOcr := DATA_DIR
	modelNameForOcr := MODEL_NAME
	ignoreSpaces := false
	recognizedValue := expectedValue := ""

	if (!CheckGeneratedModelExistence()) {
		message := "There is no newly generated model of name '" MODEL_NAME "' inside the '" DATA_DIR "' folder.`n"
		if (START_MODEL) {
			if (YesNoConfirmation(message "Do you want to use Start Model instead for this preview?")) {
				tessdataDirForOcr := TESSDATA
				modelNameForOcr := START_MODEL
			} else {
				return
			}
		} else {
			NotAllowedBox(message "Please execute training first or select a Start Model.")
			return
		}
	}

	imageList := []
	for fileExtension in SUPPORTED_IMAGE_FILES {
		ArrayPushAll(FindAllFilesExtended(GROUND_TRUTH_DIR "\*" fileExtension), imageList)
	}

	if (imageList.Length == 0) {
		NotAllowedBox("No supported images found in your Ground Truth directory")
		return
	}

	imageList := ArraySort(imageList, (a,b)=>a.modified-b.modified) 	; Sort by modificaton date descending
	imageListPosition := 1

	; Create GUI

	previewGui := Gui("-Resize -DPIScale +Owner" btnCtrl.Gui.Hwnd, "Trained Image")

	imageFullPath := imageList[imageListPosition].path
	previewGui.Add("Text", "section xm w" col1Width, "Processed image:")
	imagePathCtrl := previewGui.Add("Edit", "ys w" col2Width " r1 +ReadOnly vImagePath", imageFullPath)

	ocrResultLabelCtrl := previewGui.Add("Text", "section xm w" col1Width, "Recongized value:")
	previewGui.SetFont("s15 w678 cGreen")
	ocrResultCtrl := previewGui.Add("Edit", "ys w" col2Width " r2 +ReadOnly vOcrResult", "")
	previewGui.SetFont()

	gtTxtLabelCtrl := previewGui.Add("Text", "section xm h35 w" col1Width, "Value from .gt.txt file:")
	previewGui.SetFont("cBlue s15 w678")
	gtTxtCtrl := previewGui.Add("Edit", "ys w" col2Width " r2 +ReadOnly vGtTxtValue", "")
	previewGui.SetFont()

	previousButton := previewGui.Add("Button", "xm section default Disabled" (imageListPosition == 1), "&Previous")
	previousButton.OnEvent("Click", ShowPreviousImage)

	nextButton := previewGui.Add("Button", "ys Disabled" (imageListPosition == imageList.Length), "&Next")
	nextButton.OnEvent("Click", ShowNextImage)

	searchWrongButton := previewGui.Add("Button", "ys", "Find next &Wrong OCR")
	searchWrongButton.OnEvent("Click", FindNextWrongOcr)

	stopSearchButton := previewGui.Add("Button", "xp yp Hidden", "&Stop searching")
	stopSearchButton.OnEvent("Click", StopSearchCallback)

	closeButton := previewGui.Add("Button", "ys", "&Close")
	closeButton.OnEvent("Click", CloseCb)

	ignoreSpacesCtrl := previewGui.Add("Checkbox", "ys hp 0x20 Checked" ignoreSpaces, "Preserve interword spaces")
	ignoreSpacesCtrl.OnEvent("Click", PreserveInterwordSpaces)

	picCtrl := previewGui.Add("Picture", "xm w" (col1Width + col2Width) " h-1", imageFullPath)

	progressStatus := previewGui.Add("StatusBar",, "Preview OCR for image 1 / " imageList.Length)
	previewGui.Title := PROGRAM_TITLE " - Ground Truth Preview"

	previewGui.Show("AutoSize")  ; Resize the window to match the picture size.
	previewGui.OnEvent("Close", CloseCb)
	previewGui.OnEvent("Escape", CloseCb)
	btnCtrl.Gui.Opt("+Disabled")
	RefreshGui()

	GetGtTxtContent() {
		gtTxtPath := RemoveImageExtension(imageFullPath) ".gt.txt"
		if (FileExist(gtTxtPath)) {
			return FileGetFirstLine(gtTxtPath)
		} else {
			return "<no .gt.txt file>"
		}
	}

	RefreshGui() {
		if (closeRequest) {
			return
		}
		ocrInProgress := true

		imageFullPath := imageList[imageListPosition].path
		imagePathCtrl.Text := imageFullPath

		expectedValue := GetGtTxtContent()
		gtTxtCtrl.Text := expectedValue

		if (!searchEnabled) {
			nextButton.Enabled := (imageListPosition < imageList.Length)
			previousButton.Enabled := (imageListPosition > 1)
		}

		picCtrl.Value := "*w" (col1Width + col2Width) " *h-1 " imageFullPath  ; Load the image.

		progressStatus.SetText("Preview OCR for image " imageListPosition " / " imageList.Length)

		previewGui.Show("AutoSize")  ; Resize the window to match the picture size.

		imagePathCtrl.Focus()

		ocrResultCtrl.SetFont("norm cBlack")
		ocrResultCtrl.Text := "...recognizing..."
		recognizedValue := OcrImageFile(imageFullPath, modelNameForOcr, tessdataDirForOcr, ignoreSpaces)
		ocrResultCtrl.SetFont("bold " (CompareResults(recognizedValue, expectedValue) ? "cGreen" : "cRed"))
		ocrResultCtrl.Text := recognizedValue

		ocrInProgress := false
		if (closeRequest) {
			CloseCb()
		}
	}

	FindNextWrongOcr(searchCtrl, *) {
		ReplaceControl(searchWrongButton, stopSearchButton)
		previousButton.Enabled := false
		nextButton.Enabled := false

		searchEnabled := true
		loop {
			ShowNextImage()
		} until !searchEnabled || closeRequest || imageListPosition == imageList.Length || !CompareResults(recognizedValue, expectedValue)
		if (imageListPosition == imageList.Length) {
			AotBox("No more images")
		}
		searchEnabled := false

		if (closeRequest) {
			CloseCb()
		} else {
			ReplaceControl(stopSearchButton, searchWrongButton)
			previousButton.Enabled := true
			nextButton.Enabled := true
		}
	}
	
	ReplaceControl(controlToReplace, newControl) {
		if (closeRequest) {
			return
		}
		controlToReplace.GetPos(&x, &y, &w, &h)
		controlToReplace.Visible := false
		newControl.Visible := true
		newControl.Move(x, y, w, h)
	}
	
	StopSearchCallback(stopCtrl, *) {
		searchEnabled := false
	}

	PreserveInterwordSpaces(ctrlObj, *) {
		ignoreSpaces := ctrlObj.Value
		RefreshGui()
	}

	CompareResults(ocrResult, gtTxtValue) {
		if (ignoreSpaces) {
			ocrResult := StrReplace(ocrResult, " ")
			gtTxtValue := StrReplace(gtTxtValue, " ")
		}
		return ocrResult == gtTxtValue
	}

	ShowPreviousImage(*) {
		if (imageListPosition == 1) {
			NotAllowedBox("Already at first one")
			return
		}
		imageListPosition -= 1
		RefreshGui()
	}

	ShowNextImage(*) {
		if (imageListPosition == imageList.Length) {
			NotAllowedBox("No more files")
			return
		}
		imageListPosition += 1
		RefreshGui()
	}

	CloseCb(*) {
		closeButton.Enabled := false
		if (searchEnabled || ocrInProgress) {
			closeRequest := true
			return
		}
		btnCtrl.Gui.Opt("-Disabled")
		previewGui.Destroy()
	}

	CheckGeneratedModelExistence() {
		generatedModelFile := DATA_DIR "\" MODEL_NAME ".traineddata"
		return FileExist(generatedModelFile)
	}
}
